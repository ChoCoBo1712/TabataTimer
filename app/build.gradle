plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def getVersionCode = { name ->
    Integer code = 0
    name = name.replaceAll('v', '')
    String[] components = name.split("\\.")
    Integer i = components.size() - 1

    for (component in components) {
        code += Math.pow(1000, i) * (component as Integer)
        i--
    }

    return code
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()

    exec {
        commandLine 'git', 'describe', '--tags', '--long'
        standardOutput = stdout
    }

    stdout = stdout.toString().trim()
    stdout = stdout.replaceAll(stdout.charAt(stdout.indexOf('-')).toString(), '.')

    return stdout[0..(stdout.lastIndexOf('.') - 1)]
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.example.tabatatimer"
        minSdkVersion 23
        targetSdkVersion 29
        versionName getVersionName()
        versionCode getVersionCode(versionName)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    //default
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //room
    implementation 'androidx.room:room-runtime:2.2.6'
    kapt 'androidx.room:room-compiler:2.2.6'
    implementation 'androidx.room:room-ktx:2.2.6'
    testImplementation 'androidx.room:room-testing:2.2.6'

    //viewModels
    implementation "androidx.fragment:fragment-ktx:1.2.5"

    //coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'

    //preferences
    implementation "androidx.preference:preference-ktx:1.1.1"
}